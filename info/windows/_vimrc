let g:app_dir = get(g:, 'app_dir', $USERPROFILE . '/app')

function! s:tr_slash(s)
  return substitute(a:s, '\', '/', 'g')
endfunction
let g:app_dir = s:tr_slash(g:app_dir)

let s:path = map(split($PATH, ';'), 's:tr_slash(v:val)')
for s:i in [
      \ g:app_dir . '/vimfiles/bin',
      \ g:app_dir . '/MinGit/cmd',
      \ g:app_dir . '/MinGit/usr/bin',
      \ ]
  if index(s:path, s:i) < 0 && isdirectory(s:i)
    let $PATH = s:i . ';' . $PATH
  endif
endfor
unlet s:path
unlet s:i

for s:i in [
      \ 'C:/msys64/usr/bin/zsh', 'C:/msys64/usr/bin/bash',
      \ 'C:/msys32/usr/bin/zsh', 'C:/msys32/usr/bin/bash',
      \ 'C:/Program Files/Git/usr/bin/bash',
      \ 'C:/Program Files (x86)/Git/usr/bin/bash',
      \ ]
  if executable(s:i)
    let g:sh_path = s:i
    let $PATH = $VIMRUNTIME . ';/usr/bin;/bin;' . $PATH
    break
  endif
endfor
unlet s:i

if !exists('g:sh_path')
  let $PATH =  g:app_dir . '/bin' . ';' . $PATH
endif

let $HOME = g:app_dir
let $ENV = expand('~/vimfiles/shrc/env.sh')
let $SH_RC_LOCAL = expand('~/vimfiles/shrc/local.sh')
source ~/vimfiles/vimrc.tiny
source ~/vimfiles/vimrc.vim
silent! call plug#begin('~/vimfiles/bundle')
source ~/vimfiles/vimrc.pkgs
call plug#end()
